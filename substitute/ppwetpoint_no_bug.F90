SUBROUTINE PPWETPOINT(YDCST, YDPHY,KIDIA,KFDIA,KLON,PAPRS,PT,PQV,PQL,PQI,PWETPOINT)
USE PARKIND1  ,ONLY : JPIM     ,JPRB
USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK, JPHOOK

USE YOMPHY   , ONLY : TPHY
USE YOMCST   , ONLY : TCST  

IMPLICIT NONE

TYPE(TCST)         ,INTENT(IN)   :: YDCST
TYPE(TPHY)         ,INTENT(IN)   :: YDPHY
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KIDIA 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KFDIA 
INTEGER(KIND=JPIM) ,INTENT(IN)   :: KLON 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PAPRS(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PT(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PQV(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PQL(KLON) 
REAL(KIND=JPRB)    ,INTENT(IN)   :: PQI(KLON) 
REAL(KIND=JPRB)    ,INTENT(OUT)  :: PWETPOINT(KLON) 

REAL(KIND=JPRB) :: ZRT(KLON),ZCPT(KLON),ZRRT(KLON),ZS1(KLON),ZCONS(KLON)&
 & ,ZTITER(KLON),ZDEL(KLON),ZS2(KLON),ZFUNCT(KLON),ZPITER(KLON)  

INTEGER(KIND=JPIM) :: JIT, JLON

REAL(KIND=JPRB) :: ZCPLNTT, ZCWS, ZDELTA, ZDF, ZDLEW, ZDLSTT,&
 & ZE, ZEPSP, ZEPSRH, ZEW, ZF, ZKAPPA, ZKDI, &
 & ZKDW, ZKNI, ZKNW, ZLH, ZLHZ, ZLHZI, ZLHZW, &
 & ZLSTC, ZLSTT, ZLSTTI, ZLSTTW, ZPRS, ZQI, &
 & ZQL, ZQV, ZQVSAT, ZRDSV, ZRI, ZRL, ZRS, ZRV, &
 & ZTIN, ZTMAX, ZTMIN  
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#include "fcttrm.func.h"
IF (LHOOK) CALL DR_HOOK('PPWETPOINT',0,ZHOOK_HANDLE)
ASSOCIATE(NBITER=>YDPHY%NBITER, LNEIGE=>YDPHY%LNEIGE, &
 & RCPD=>YDCST%RCPD, RCPV=>YDCST%RCPV, RCS=>YDCST%RCS, RCW=>YDCST%RCW, RD=>YDCST%RD, &
 & RESTT=>YDCST%RESTT, RKAPPA=>YDCST%RKAPPA, RTT=>YDCST%RTT, RV=>YDCST%RV)

ZRDSV=RD/RV
ZLSTTW= FOLH (RTT,0.0_JPRB)/RTT+RCW*RV/( FOLH (RTT,0.0_JPRB)/RTT)
ZLSTTI= FOLH (RTT,1.0_JPRB)/RTT+RCS*RV/( FOLH (RTT,1.0_JPRB)/RTT)
ZLHZW= FOLH (0.0_JPRB,0.0_JPRB)
ZLHZI= FOLH (0.0_JPRB,1.0_JPRB)
ZKNW=RESTT* FOLH (RTT,0.0_JPRB)/(RV*RTT)
ZKNI=RESTT* FOLH (RTT,1.0_JPRB)/(RV*RTT)
ZKDW=RKAPPA*RESTT*( FOLH (RTT,0.0_JPRB)/(RV*RTT))**2
ZKDI=RKAPPA*RESTT*( FOLH (RTT,1.0_JPRB)/(RV*RTT))**2
ZCPLNTT=RCPD*LOG(RTT)

ZEPSP=10._JPRB
ZEPSRH=0.001_JPRB
ZTMIN=155._JPRB
ZTMAX=355._JPRB


DO JLON=KIDIA,KFDIA
  IF (LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-PT(JLON)))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF

  ZQL=MAX(0.0_JPRB,PQL(JLON))
  ZQI=MAX(0.0_JPRB,PQI(JLON))
  ZTIN=MAX(ZTMIN,MIN(ZTMAX,PT(JLON)))
  ZPRS=MAX(ZEPSP,PAPRS(JLON))
  ZQVSAT=FOQS(FOEW(ZTIN,ZDELTA)/ZPRS)
  ZQV=MAX(ZEPSRH*ZQVSAT,PQV(JLON))

  ZRV=ZQV/(1.0_JPRB-ZQV-ZQL-ZQI)
  ZRL=ZQL/(1.0_JPRB-ZQV-ZQL-ZQI)
  ZRI=ZQI/(1.0_JPRB-ZQV-ZQL-ZQI)
  ZRT(JLON)=ZRV+ZRL+ZRI
  ZCPT(JLON)=RCPD+RCPV*ZRT(JLON)
  ZRRT(JLON)=RD+RV*ZRT(JLON)
  ZE=(ZRV*ZPRS)/(ZRV+ZRDSV)
  ZS1(JLON)=ZCPT(JLON)*LOG(ZTIN)-RD*LOG(ZPRS-ZE)-RV*ZRT(JLON)&
   & *LOG(ZE)-( FOLH (ZTIN,0.0_JPRB)*ZRL+ FOLH (ZTIN,1.0_JPRB)*ZRI)/ZTIN  
  ZCONS(JLON)=ZS1(JLON)+RD*LOG(ZRDSV/ZRT(JLON))
  ZTITER(JLON)=ZTIN
ENDDO
DO JIT=1,NBITER
  DO JLON=KIDIA,KFDIA
    IF (LNEIGE) THEN
      ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTITER(JLON)))
    ELSE
      ZDELTA=0.0_JPRB
    ENDIF

    ZEW= FOEW (ZTITER(JLON),ZDELTA)
    ZDLEW= FODLEW (ZTITER(JLON),ZDELTA)
    ZF=ZCONS(JLON)+ZRRT(JLON)*LOG(ZEW)-ZCPT(JLON)*LOG(ZTITER(JLON))
    ZDF=ZRRT(JLON)*ZDLEW-ZCPT(JLON)/ZTITER(JLON)
    ZTITER(JLON)=ZTITER(JLON)-ZF/ZDF
  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
  IF (LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,RTT-ZTITER(JLON)))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF

  ZDEL(JLON)=ZDELTA
  ZEW= FOEW (ZTITER(JLON),ZDELTA)
  ZLSTT=ZLSTTW+ZDELTA*(ZLSTTI-ZLSTTW)
  ZCWS=RCW+ZDELTA*(RCS-RCW)
  ZLSTC= FOLH (ZTITER(JLON),ZDELTA)/ZTITER(JLON)+ZCWS*RV&
   & /( FOLH (ZTITER(JLON),ZDELTA)/ZTITER(JLON))  
  ZFUNCT(JLON)=ZRDSV*RESTT*ZLSTT
  ZS2(JLON)=ZS1(JLON)+ZRT(JLON)*(ZLSTC+RV*LOG(ZEW)-RCPV*LOG(ZTITER(JLON)))
  ZCONS(JLON)=ZS2(JLON)-ZCPLNTT
  ZKAPPA=RKAPPA*(1.0_JPRB+ZRT(JLON)* FOLH (ZTITER(JLON),ZDELTA)/(RD&
   & *ZTITER(JLON)))/(1.0_JPRB+RKAPPA*ZRT(JLON)&
   & * FOLH (ZTITER(JLON),ZDELTA)**2/(RD*RV*ZTITER(JLON)**2))  
  ZPITER(JLON)=(ZRDSV*ZEW/ZRT(JLON))*(RTT/ZTITER(JLON))**(1.0_JPRB/ZKAPPA)-RESTT
  ZPITER(JLON)=MAX(ZPITER(JLON),ZEPSP)
ENDDO
DO JIT=1,NBITER+1
  DO JLON=KIDIA,KFDIA
    ZF=ZCONS(JLON)+RD*LOG(ZPITER(JLON))-ZFUNCT(JLON)/ZPITER(JLON)
    ZDF=(RD*ZPITER(JLON)+ZFUNCT(JLON))/ZPITER(JLON)**2
    ZPITER(JLON)=ZPITER(JLON)-ZF/ZDF
  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
  ZPITER(JLON)=ZPITER(JLON)+RESTT
ENDDO
DO JLON=KIDIA,KFDIA
  IF (LNEIGE) THEN
    ZDELTA=MAX(0.0_JPRB,SIGN(1.0_JPRB,ZPITER(JLON)-PAPRS(JLON)))
  ELSE
    ZDELTA=0.0_JPRB
  ENDIF

  ZDLSTT=(ZDELTA-ZDEL(JLON))*(ZLSTTI-ZLSTTW)
  ZDEL(JLON)=ZDELTA
  ZS2(JLON)=ZS2(JLON)+ZDLSTT*ZRDSV*RESTT/(ZPITER(JLON)-RESTT)
  ZKAPPA=RKAPPA*(1.0_JPRB+(ZKNW+ZDELTA*(ZKNI-ZKNW))/ZPITER(JLON))/(1.0_JPRB&
   & +(ZKDW+ZDELTA*(ZKDI-ZKDW))/ZPITER(JLON))  
  ZTITER(JLON)=RTT*(PAPRS(JLON)/ZPITER(JLON))**ZKAPPA
ENDDO
DO JIT=1,NBITER
  DO JLON=KIDIA,KFDIA
    ZEW= FOEW (ZTITER(JLON),ZDEL(JLON))
    ZCWS=RCW+ZDEL(JLON)*(RCS-RCW)
    ZLHZ=ZLHZW+ZDEL(JLON)*(ZLHZI-ZLHZW)
    ZLH= FOLH (ZTITER(JLON),ZDEL(JLON))
    ZLSTC=ZLH/ZTITER(JLON)+ZCWS*RV/(ZLH/ZTITER(JLON))
    ZRS=ZRDSV*ZEW/(PAPRS(JLON)-ZEW)
    ZF=ZS2(JLON)-RCPD*LOG(ZTITER(JLON))+RD*LOG(PAPRS(JLON)-ZEW)-ZRS*ZLSTC
    ZDF=-RCPD/ZTITER(JLON)-ZRS*((PAPRS(JLON)/(PAPRS(JLON)-ZEW))*ZLSTC*ZLH/(RV&
     & *ZTITER(JLON)**2)+ZCWS*RV*ZLHZ/ZLH**2+(RCPV-ZCWS)&
     & /ZTITER(JLON))  
    ZTITER(JLON)=ZTITER(JLON)-ZF/ZDF
  ENDDO
ENDDO
DO JLON=KIDIA,KFDIA
  PWETPOINT(JLON)=ZTITER(JLON)
ENDDO

END ASSOCIATE
IF (LHOOK) CALL DR_HOOK('PPWETPOINT',1,ZHOOK_HANDLE)
END SUBROUTINE PPWETPOINT

